#version 450
#extension GL_EXT_shader_explicit_arithmetic_types_float64 : enable

#define PI 3.14159265359
#define FLT_MIN 1.175494e-38

#define DIFFUSE         0
#define SUBSURFACE      1
#define SPECULAR        2
#define METAL           3
#define TRANSMISSION    4
#define EMISSION        5
#define TRANSPARENT     6
#define VOID            7

#define AMBIENT         0
#define SPHERE          1
#define POINT           2
#define DIRECTIONAL     3
#define CONE            4
#define AREA            5



// ------------ Struct definitions --------------
struct Camera{
    mat4 view;
    mat4 viewInv;
    mat4 proj;
    mat4 projInv;
    mat4 viewproj;
    vec3 position;
    float tanHalfFOV;
};

struct Ray{
    vec3 orig;
    vec3 dir;
};

struct Interval{
    float minV;
    float maxV;
};

struct Light{
    vec4 pos_angle_aux;
    vec4 color_str;
    int type;
    float accumulated_str;
};

struct Material{
    vec4 albedo;            // Surface color rgb. Alpha controls opacity: 0.0 = transparent 1.0 = opaque

    // Subsurface | Method: Christensen-Burley
    vec4 subsurface;        // Average distance the ligths scatter below the surface
                            // Alpha controls weight: 0.0 = diffuse , 1.0 = subsurface

    // Specular | Method: GGX
    vec4 specular_tint;     // Color tint for specular and metalic relfections
                            // Alpha controls IOR Level: 
                            // 0.0 = no reflections , 0.5 = no adjustment, 1.0 = double reflections

    // Emission
    vec4 emission_color;    // Color of the light emited. Alpha controls strength

    // General
    float roughness;        // 0.0 = smooth , 1.0 = rough
    float metallic;         // 0.0 = dielectric , 1.0 = metalic
    float ior;              // Index of refraction

    // Transmission
    float trs_weight;       // 0.0 = opaque , 1.0 = transmisive

    // Coat
    // TODO

    // Sheen
    // TODO
};

struct Sphere{
    vec3 pos; // Coordinates of the center
    float r; // Radius
    int mat; // Material index
};

struct Triangle{
    vec3 v0;
    vec3 v1;
    vec3 v2;
    vec3 normal;
    int mat;
};

struct MeshInfo{
    int index_start;
    int index_end;
    int material;
};

struct Vertex{
    vec3 pos;
};

struct Hit{
    vec3 p; // Where it happend
    vec3 normal; // The normal where it hit
    int mat; // Material index of the object it hit
    float t; // The distance from the ray origin to the hit
    bool front_face; // True if hit is to a front facing surface
};

// ------------ Constant definitions --------------
const int rays_per_pixel = 20;
const int max_bounces = 10;


const float PINF = 1.0 / 0.0;
const float NINF = -1.0 / 0.0;
const Interval empty_interval = Interval(PINF, NINF);
const Interval universe_interval = Interval(NINF, PINF);

// ------------ External memory layout --------------
layout(push_constant) uniform PushConstants {
    float time;
    uint frameCount;
    int total_lights;
    float lights_strength_sum;
    vec3 world_up;
    bool reset_frame_accumulation;
} pc;

layout(set = 0, binding = 0) uniform UniformBufferObject {
    Camera camera;
} ubo;

layout(set = 1, binding = 0, rgba8) uniform writeonly image2D outputImage;

layout(set = 1, std430, binding = 1) buffer SpheresSSBOOut {
    Sphere spheres[];
};

layout(set = 1, std430, binding = 2) buffer MaterialsSSBOOut {
    Material materials[];
};

layout(set = 1, std430, binding = 3) buffer LightsSSBOOut {
    Light lights[];
};

layout(set = 1, std430, binding = 4) buffer TrianglesSSBOOut {
    Triangle triangles[];
};

layout(set = 1, std430, binding = 5) buffer VertexSSBOOut {
    Vertex vertices[];
};

layout(set = 1, std430, binding = 6) buffer IndicesSSBOOut {
    uint indices[];
};

layout(set = 1, std430, binding = 7) buffer ModelsSSBOOut {
    MeshInfo meshes[];
};

layout(set = 2, std430, binding = 0) buffer ColorAccumulationSSBOInOut {
    vec4 accumulated_colors[];
};

layout(set = 2, std430, binding = 1) buffer SampleCountSSBOInOut {
    int sample_counts[];
};

// ------------ Workgroup sizes --------------
layout(local_size_x = 32, local_size_y = 32) in;

// Global variables
ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);
ivec2 imageSize = imageSize(outputImage);
float aspectRatio = float(imageSize.x)/float(imageSize.y);

uint seed;


// ------------ RNG functions --------------
uint hash(uint x) {
    x ^= x >> 16;
    x *= 0x7feb352dU;
    x ^= x >> 15;
    x *= 0x846ca68bU;
    x ^= x >> 16;
    return x;
}

uint xorshift(inout uint state) {
    state ^= state << 13;
    state ^= state >> 17;
    state ^= state << 5;
    return state;
}

float random(){
    return float(xorshift(seed)) / 4294967295.0; // Dividir por 2^32 - 1
}

float random_bound(float minV, float maxV){
    return minV + (maxV-minV)*random();
}

vec3 randomVec(){
    return vec3(random(),random(),random());
}

vec3 random_vec_bound(float minV, float maxV){
    return vec3(random_bound(minV,maxV),random_bound(minV,maxV),random_bound(minV,maxV));
}

vec3 random_unit_vec(){
    float phi = 2.0 * PI * random();
    float theta = acos(2.0 * random() - 1.0);
    float sin_theta = sin(theta);
    return vec3(
        sin_theta * cos(phi),
        sin_theta * sin(phi),
        cos(theta)
    );
}

vec3 random_vec_on_hemisphere(vec3 normal){
    vec3 rvec = random_unit_vec();
    if(dot(rvec,normal) > 0.0){
        return rvec;
    }else{
        return -rvec;
    }
}

vec2 sample_square(){
    return vec2(random()-0.5,random()-0.5);
}

// ------------ Math functions --------------
float power_heuristics(float a, float b){
    float a2 = a*a;
    float b2 = b*b;
    return a2 / max((a2+b2),0.000001);
}

float balance_heuristics(float a, float b){
    return a / max((a+b),0.000001);
}

vec3 sphere_to_cartesian(float theta, float phi){
    float sin_t = sin(theta);
    return vec3(
        sin_t*cos(phi),
        sin_t*sin(phi),
        cos(theta)
    );
}

void cartesian_to_sphere(vec3 direction, out float theta, out float phi){
    theta = acos(direction.z);
    phi = atan(direction.y, direction.x);
}

float vecs_to_angle(vec3 a, vec3 b){
    return acos(dot(a,b)/length(a)/length(b));
}

// Aligns a local direction vector X to world space using normal N as reference
vec3 align_to_world(vec3 X, vec3 N){
    vec3 up;
    // Aproximation for speed, not 100% orthonormal when N is near +Z
    if(abs(N.z) > 0.9999999){
        up = vec3(1.0,0.0,0.0);
    }else{
        up = vec3(0.0,0.0,1.0);
    }

    vec3 T = normalize(cross(up,N));
    vec3 B = cross(N,T);

    return T*X.x + B*X.y + N*X.z;
}


// ------------ Ray functions --------------
// Having the length of a hit (t) returns the point where it hit
vec3 at(const Ray r, const float t){
    return r.orig + t*r.dir;
}

vec3 lambertian_diffuse(const vec3 normal){
    return normalize(normal + random_unit_vec());
}


// ------------ Interval functions --------------
bool contains(Interval i, float x){
    return i.minV <= x && i.maxV >= x;
}
bool surrounds(Interval i, float x){
    return i.minV < x && i.maxV > x;
}
float clamp_interval(Interval i, float x){
    if(x < i.minV) return i.minV; 
    if(x > i.maxV) return i.maxV;
    return x;
}

// ------------ Hit record functions --------------
// Having the outward facing normal of the surface hit
// updates the record frontFace and normal to correct values
void set_face_normal(inout Hit h, const Ray r, const vec3 outward_normal){
    h.front_face = dot(r.dir,outward_normal) < 0;
    if(h.front_face){
        h.normal = outward_normal;
    }else{
        h.normal = -outward_normal;
    }
}

// ------------ Spheres functions --------------
// Returns true if the ray colides with the sphere
// If it hits it fills out th hit record
bool hit_sphere(const Sphere s, const Interval ray_t, const Ray r, out Hit rec){
    vec3 oc = s.pos - r.orig;
    float a = dot(r.dir,r.dir);
    float h = dot(r.dir, oc);
    float c = dot(oc,oc) - s.r*s.r;
    float discriminant = h*h - a*c;
    if (discriminant < 0) {
        return false;
    } 

    float srtd = sqrt(discriminant);

    float root = (h - srtd) / a;
    if(!surrounds(ray_t,root)){
        root = (h + srtd) / a;
        if(!surrounds(ray_t,root)){
            return false;
        }
    }

    rec.t = root;
    rec.p = at(r,root);
    rec.mat = s.mat;
    vec3 outward_normal = (rec.p - s.pos) / s.r;
    set_face_normal(rec, r, outward_normal);

    return true;
}

// ------------ Triangle functions --------------
// Returns true if the ray colides with the triangle
// If it hits it fills out th hit record
bool hit_triangle(const Triangle t, const Interval ray_t, const Ray r, out Hit rec){
    const float EPSILON = 1e-6;
    vec3 edge1 = t.v1 - t.v0;
    vec3 edge2 = t.v2 - t.v0;
    vec3 h = cross(r.dir, edge2);
    float a = dot(edge1, h);

    if (abs(a) < EPSILON) {
        return false;
    }

    float f = 1.0 / a;
    vec3 s = r.orig - t.v0;
    float u = f * dot(s, h);

    if (u < 0.0 || u > 1.0) {
        return false;
    }

    vec3 q = cross(s, edge1);
    float v = f * dot(r.dir, q);

    if (v < 0.0 || u + v > 1.0) {
        return false;
    }

    float t_r = f * dot(edge2, q);

    if (!surrounds(ray_t, t_r)) {
        return false;
    }

    rec.t = t_r;
    rec.p = at(r, t_r);
    rec.mat = t.mat;

    vec3 outward_normal = t.normal;
    set_face_normal(rec, r, outward_normal);

    return true;
}

bool hit_mesh(const MeshInfo mesh_info, const Interval ray_t, const Ray r, out Hit rec){
    bool hit_anything = false;
    float closest_so_far = ray_t.maxV;
    
    for(int i = mesh_info.index_start; i < mesh_info.index_end; i+=3){
        Vertex v0 = vertices[indices[i]];
        Vertex v1 = vertices[indices[i+1]];
        Vertex v2 = vertices[indices[i+2]];
        const float EPSILON = 1e-6;
        vec3 edge1 = v1.pos - v0.pos;
        vec3 edge2 = v2.pos - v0.pos;
        vec3 h = cross(r.dir, edge2);
        float a = dot(edge1, h);

        if (abs(a) < EPSILON) {
            continue;
        }

        float f = 1.0 / a;
        vec3 s = r.orig - v0.pos;
        float u = f * dot(s, h);

        if (u < 0.0 || u > 1.0) {
            continue;
        }

        vec3 q = cross(s, edge1);
        float v = f * dot(r.dir, q);

        if (v < 0.0 || u + v > 1.0) {
            continue;
        }

        float t_r = f * dot(edge2, q);

        if (!surrounds(ray_t, t_r)) {
            continue;
        }

        if(t_r>closest_so_far){
            continue;
        }

        closest_so_far = t_r;
        hit_anything = true;
        
        rec.t = t_r;
        rec.p = at(r, t_r);

        vec3 outward_normal = normalize(cross(edge1,edge2));
        set_face_normal(rec, r, outward_normal);
    }

    rec.mat = mesh_info.material;

    return hit_anything;
}

// ------------ Scene functions --------------
// Calculates the hit record for the ray
// ray_tmin and ray_tmax to be investigated
bool hit_scene(const Ray r, const Interval ray_t, inout Hit rec){
    Hit temp_rec;
    bool hit_anything = false;
    float closest_so_far = ray_t.maxV;

    // For every sphere in the scene
    for(int i = 0; i< spheres.length(); i++){
        if(hit_sphere(spheres[i],ray_t,r,temp_rec)){
            hit_anything = true;
            if(closest_so_far > temp_rec.t){
                closest_so_far = temp_rec.t;
                rec = temp_rec;
            }
        }
    }

    // For every tri in the scene
    for(int i = 0; i< triangles.length(); i++){
        if(hit_triangle(triangles[i],ray_t,r,temp_rec)){
            hit_anything = true;
            if(closest_so_far > temp_rec.t){
                closest_so_far = temp_rec.t;
                rec = temp_rec;
            }
        }
    }

    // For every model in the scene
    for(int i = 0; i< meshes.length(); i++){
        if(hit_mesh(meshes[i],ray_t,r,temp_rec)){
            hit_anything = true;
            if(closest_so_far > temp_rec.t){
                closest_so_far = temp_rec.t;
                rec = temp_rec;
            }
        }
    }

    return hit_anything;
}

// Returns true if ray has hit anything in the scene
bool shadow_ray(const Ray r){
    Hit h;
    Interval i = Interval(0.005, PINF);
    return hit_scene(r,i,h);
}



// ------------ Skybox functions --------------

// Skybox variation for a nice and blue radiant day at the park
vec4 skybox_color_day(Ray r) {
    const vec3 sun_dir = vec3(-0.33, 0.67, -0.67);
    const vec4 sun_color = vec4(1.0);
    const float sun_size = 0.999;
    const float light_bleed = 0.0003;
    const vec4 horizon_color = vec4(0.231, 0.756, 0.945, 1.0);
    const vec4 zenith_color = vec4(1.0);

    vec3 dir_unit = normalize(r.dir);
    float a = 0.5 * (dir_unit.y + 1.0); 
    vec4 sky_gradient = mix(horizon_color, zenith_color, a);

    float sun_dot = dot(sun_dir, dir_unit);
    float sun_mask = smoothstep(sun_size - light_bleed, sun_size + light_bleed, sun_dot);

    return mix(sky_gradient, sun_color, sun_mask);
}

// Skybox variation for moody and relaxed moon light shaped sky
vec4 skybox_color_night(Ray r) {
    const vec3 moon_dir = normalize(vec3(0.33, 0.67, -0.67));
    const vec4 moon_color = vec4(0.9, 0.9, 0.8, 1.0); 
    const float moon_size = 0.999;
    const float light_bleed = 0.0003;
    const vec4 horizon_color = vec4(0.0);
    const vec4 zenith_color = vec4(0.005, 0.005, 0.005, 1.0); 

    vec3 dir_unit = normalize(r.dir);
    float a = 0.5 * (dir_unit.y + 1.0); 
    vec4 sky_gradient = mix(horizon_color, zenith_color, a);

    float moon_dot = dot(moon_dir, dir_unit);
    float moon_mask = smoothstep(moon_size - light_bleed, moon_size + light_bleed, moon_dot);

    return mix(sky_gradient, moon_color, moon_mask);
}

// Skybox variation for just a white sky
vec4 skybox_color_white(Ray r){
    return vec4(1.0);
}

// Skybox variation for just a black sky
vec4 skybox_color_black(Ray r){
    return vec4(0.0);
}

// Color of the skybox where the ray is pointing to
vec4 skybox_color(Ray r) {
    return skybox_color_day(r);
}


// ------------ Lighting functions --------------
// Strength based importance sampling. Returns radiance of the light 
vec3 sample_light(vec3 point, vec3 normal, out vec3 L, out float pdf){
    if (pc.total_lights == 0 || pc.lights_strength_sum <= 0.0) {
        pdf = 0.0;
        return vec3(0.0);
    }
    // Choose light to sample with ponderated sampling in strength and a binary search
    float rand_strength = random() * pc.lights_strength_sum;
    int low = 0, high = pc.total_lights - 1;
    while (low < high) {
        int mid = (low + high) / 2;
        if (rand_strength <= lights[mid].accumulated_str) {
            high = mid;
        } else {
            low = mid + 1;
        }
    }
    Light picked_light = lights[low];

    switch(picked_light.type){
        case AMBIENT:
            L = random_vec_on_hemisphere(normal);
            pdf = 1 / 2.0 / PI;
            return picked_light.color_str.rgb;
            break;
        case SPHERE:
            vec3 center = picked_light.pos_angle_aux.xyz;
            float radius = picked_light.pos_angle_aux.w;
            vec3 center_to_point = point-center;
            vec3 sphere_point = random_vec_on_hemisphere(normalize(center_to_point)) * radius + center;
            vec3 point_to_spoint = sphere_point-point;
            float d_to_spoint = length(point_to_spoint);
            L = normalize(point_to_spoint);
            Ray s_ray = Ray(point,L);
            Hit h;
            Interval i = Interval(0.005, PINF);
            if(hit_scene(s_ray,i,h)){
                if(abs(d_to_spoint - h.t) < 0.1){
                    float d2 = d_to_spoint*d_to_spoint;
                    pdf = 1 / 2.0 / PI;
                    return clamp(picked_light.color_str.rgb * picked_light.color_str.a / d2,0.0,1.0);
                }
            }
            L = normal;
            pdf = 0.00001;
            return vec3(0.0);
            break;
        case POINT:

            break;
        case DIRECTIONAL:
            // If surface does not cover the directional light
            vec3 light_dir = -picked_light.pos_angle_aux.xyz;
            if(dot(normal,light_dir) > 0.0){
                Ray s_ray = Ray(point,light_dir);
                if(!shadow_ray(s_ray)){
                    L = light_dir;
                    pdf = 1.0;
                    return picked_light.color_str.rgb;
                }
            }
            L = normal;
            pdf = 0.00001;
            return vec3(0.0);
            break;
        case CONE:

            break;
        case AREA:

            break;
        default:

            break;
    }

    return vec3(1.0);
}


// Fresnel-Schlick aproximation to reflectance
float reflectance(float cos_theta, float F0) {
    return clamp(F0 + (1.0 - F0) * pow(1.0 - cos_theta, 5.0), 0.0, 1.0);
}
vec3 reflectance(float cos_theta, vec3 F0) {
    return clamp(F0 + (1.0 - F0) * pow(1.0 - cos_theta, 5.0), 0.0, 1.0);
}

// Fresnel-Schlick aproximation to reflectance for dielectrics
float fresnel_dielectric(float cos_theta_i, float eta) {
    float r0 = (1.0 - eta) / (1.0 + eta);
    float F0 = r0 * r0;
    return F0 + (1.0 - F0) * pow(1.0 - cos_theta_i, 5.0);
}


// Normal distribution function. GGX
float ggx_distribution(float alpha, vec3 N, vec3 H){
    float alpha_squared = alpha * alpha;
    float dot_product = dot(N, H);
    if(dot_product == 0) dot_product = 0.000001;
    float x = dot_product * dot_product * (alpha_squared - 1.0) + 1.0;
    return alpha_squared / (PI * x * x);
}


// Geometry shadowing function. Schilick-Beckmann
float G1_CT(vec3 X, vec3 N, float alpha){
    float NdotX = dot(N,X);
    float k = alpha / 2.0;
    return NdotX / max(0.000001,(NdotX*(1.0 - k) + k));
}

// Geometry shadowing function. GGX
float G1_GGX(vec3 v, vec3 N, vec3 H, float alpha){
    float voN = dot(v, N);
    if(voN == 0) voN = 0.0000001;
    float voH = dot(v, H);

    //if(voH/voN < 0.0) return 0.0;

    float tan_theta_x = tan(acos(voN));
    float alpha_tan = alpha * tan_theta_x;
    
    return 2.0 / (1.0 + sqrt(1.0 + alpha_tan * alpha_tan));
}






// ------------ Direction sampling functions --------------
// Samples a GGX-distributed microfacet normal and returns the half direction H
vec3 sample_ggx(float roughness, vec3 V, vec3 N){
    float e1 = random(), e2 = random();
    float alpha = roughness * roughness;
    //float theta = atan(alpha * sqrt(e1) / max(0.000001,sqrt(1.0 - e1)));
    float theta = acos(sqrt( (1.0 - e1) / (1.0 + (alpha - 1.0)*e1) ));
    //float theta = atan(sqrt(-alpha*log(1.0-e1)));
    float phi = 2.0 * PI * e2;

    vec3 H_tan = sphere_to_cartesian(theta, phi);
    vec3 H = align_to_world(H_tan,N);
    if (dot(V, H) < 0.0) H = -H;
    return normalize(H);
}

// Samples a reflected direction of V into N 
vec3 sample_r(Material mat, vec3 V, vec3 N){
    vec3 H = sample_ggx(mat.roughness,V,N);
    return reflect(-V,H);
}

// Samples a refracted direction of V into N 
vec3 sample_t(Material mat, float eta, vec3 V, vec3 N){
    vec3 H = sample_ggx(mat.roughness,V,N);

    float cos_theta = min(1.0,dot(V,H));
    float sin_theta = sqrt(1.0 - cos_theta*cos_theta);
    bool cannot_refract = eta * sin_theta > 1.0;

    float reflectance = fresnel_dielectric(cos_theta,eta);

    if(cannot_refract || reflectance > random()){
        return reflect(-V,H);
    }else{
        return refract(-V,H,eta);
        float c = dot(V,H);
        float x = sign(dot(V,N))*sqrt(1.0 + eta*(c*c - 1.0));
        return (eta*c-x)*H - eta*V;
    }
}

// Samples an outgoing light direction from the material
vec3 sample_mat(Material mat, vec3 V, Hit rec) {
    if(mat.trs_weight < random()) return normalize(sample_r(mat, V, rec.normal));

    float eta_i = rec.front_face ? 1.0 : mat.ior;
    float eta_o = rec.front_face ? mat.ior : 1.0;
    float eta = eta_i / eta_o;
    
    return normalize(sample_t(mat,eta, V, rec.normal));
}


// ------------ Evaluation functions --------------
vec3 eval_brdf(Material mat, vec3 L, vec3 V, Hit rec, out float pdf){
    vec3 N = rec.normal;
    float NdotL = dot(L,N);
    float NdotV = dot(V,N);
    vec3 H = sign(NdotV)*normalize(L+V);
    float VdotH = dot(V,H);
    float NdotH = dot(N,H);
    
    float ior_scale = mix(0.0, 2.0, mat.specular_tint.a);
    float ri = rec.front_face ? (1.0/(mat.ior*ior_scale)) : mat.ior*ior_scale;

    float dielectric_F0 = (1.0 - ri) / (1.0 + ri);
    vec3 dielectric_F0_vec = vec3(dielectric_F0*dielectric_F0);

    vec3 F0 = mix(dielectric_F0_vec, mat.albedo.xyz, mat.metallic);
    
    vec3 f_diffuse = mat.albedo.xyz / PI;

    float alpha = mat.roughness * mat.roughness;
    float D = ggx_distribution(alpha,N,H);
    float G = G1_GGX(L,N,H,alpha) * G1_GGX(V,N,H,alpha);
    vec3  F = reflectance(VdotH, F0);

    float ks = max(max(F.r, F.g), F.b);
    float kd = (1.0 - ks) * (1.0 - mat.metallic);

    float jacobian = 1.0 / max(0.00001,(4.0*NdotV*NdotL));
    
    vec3 f_specular = mat.specular_tint.rgb * D*G*F * jacobian ;

    float pdf_specular = clamp(D * NdotH * jacobian,0.0,1.0);
    float pdf_diffuse = clamp(NdotL / PI,0.0,1.0);
    pdf = kd * pdf_diffuse + ks * pdf_specular;

    return kd*f_diffuse + f_specular;
}

vec3 eval_btdf(Material mat, vec3 L, vec3 V, Hit rec, out float pdf){
    L = normalize(L);
    V = normalize(V);
    vec3 N = normalize(rec.normal);

    float eta_i = rec.front_face ? 1.0 : mat.ior;
    float eta_o = rec.front_face ? mat.ior : 1.0;
    float eta = eta_i / eta_o;

    vec3 H = -normalize(L+eta*V);

    float VoH = dot(V, H);
    float LoH = dot(L, H);
    float NoH = dot(N, H);
    float VoN = dot(V, N);
    float LoN = dot(L, N);

    float alpha = mat.roughness * mat.roughness;
    float D = ggx_distribution(alpha,N,H);
    float G = G1_GGX(L,N,H,alpha) * G1_GGX(V,N,H,alpha);
    float F = fresnel_dielectric(abs(VoH),eta);

    float x = abs(VoH) / max(0.00001, abs(VoN) * abs(LoN));
    float denom = (eta_i * VoH + eta_o * LoH);
    float jacobian = (eta_o * eta_o * abs(LoH)) / max(0.00001, (denom * denom));

    pdf = D * abs(NoH) * jacobian;

    return mat.subsurface.rgb *  x*jacobian * D*G*(1.0-F);;
}


// Returns whats the tint that mat gives from L to V
vec3 eval_mat(Material mat, vec3 L, vec3 V, Hit rec, out float pdf) {
    L = normalize(L);
    vec3 N = normalize(rec.normal);
    if(dot(L,N) >= 0.0){
        return eval_brdf(mat, L, V, rec, pdf);
    }else{
        return eval_btdf(mat, L, V, rec, pdf);
    }
}

// Computes direct lighting contribution at a hit point
vec3 direct_light(Hit rec, Ray ray, float pdf){
    vec3 L_emission, L_dir, fr;
    float cos_theta, light_pdf, mat_pdf;

    L_emission = sample_light(rec.p,rec.normal,L_dir,light_pdf);
    cos_theta = max(0.0,dot(rec.normal,L_dir));
    fr = eval_mat(materials[rec.mat], L_dir, -ray.dir, rec, mat_pdf);

    pdf = power_heuristics(light_pdf,mat_pdf);

    return clamp(L_emission * fr * cos_theta / pdf, 0.0, 1.0);
}


// Calculates the color of the ray by tracing it with the scene
vec4 ray_color(Ray r){
    vec3 color = vec3(0.0);
    vec3 attenuation = vec3(1.0);
    Hit h;
    
    for (int bounce = 0; bounce <= max_bounces; bounce++) {
        if (hit_scene(r, Interval(0.005, PINF), h)) {
            Material mat = materials[h.mat];

            // Transparency check
            if(mat.albedo.a < 1.0 && mat.albedo.a < random()){
                r.orig = h.p;
                continue;
            }

            // If material is emissive stop casting
            if(mat.emission_color.a > 0.0){
                color += attenuation * mat.emission_color.rgb;
                break;
            }

            // Get the direct light contribution
            if(bounce == 0){
                float light_pdf;
                color += direct_light(h,r,light_pdf) * attenuation; 
            }

            // Get the indirect light contribution
            vec3 bounce_dir = sample_mat(mat, -r.dir, h);
            float mat_pdf;
            vec3 fr = eval_mat(mat,bounce_dir,-r.dir,h,mat_pdf);
            float cos_theta = abs(dot(h.normal, bounce_dir));
            attenuation *= max(vec3(0.0),fr * cos_theta / max(0.00001,mat_pdf));

            // Prepare next ray to cast
            r.orig = h.p;
            r.dir = bounce_dir;
        } else {
            // Ray has hit the skybox
            color += attenuation * skybox_color(r).rgb;
            return vec4(clamp(color, 0.0, 1.0),1.0);
        }
    }

    return vec4(clamp(color, 0.0, 1.0),1.0);
}

// Generates a ray pointing to the pixel this thread is assign with a random half-pixel (square) offset
Ray get_ray(){
    vec2 pixelCoordsOffset = pixelCoords + sample_square();

    float ndcX = 2.0 * pixelCoordsOffset.x / imageSize.x - 1.0;
    float ndcY = 1.0 - 2.0 * pixelCoordsOffset.y / imageSize.y;

    vec3 rayDirCameraSpace = normalize(vec3(
        ndcX * aspectRatio * ubo.camera.tanHalfFOV,
        ndcY * ubo.camera.tanHalfFOV,
        -1.0
    ));

    vec3 rayDir = normalize(vec3(ubo.camera.viewInv * vec4(rayDirCameraSpace, 0.0)));

    Ray ray;
    ray.orig = ubo.camera.position;
    ray.dir = rayDir;

    return ray;
}



void main() {

    // RNG seed, will change after each generation of number
    seed = hash(uint(pc.time)*1920) 
            ^ hash(pc.frameCount)
            ^ hash(uint(pixelCoords.x + pixelCoords.y * 1920));
    /*hash(floatBitsToUint(ubo.camera.view[0][0])) 
          ^ hash(floatBitsToUint(ubo.camera.view[1][1])) 
          ^ hash(floatBitsToUint(ubo.camera.view[2][2])) 
          ^ hash(floatBitsToUint(ubo.camera.view[0][1])) 
          ^ hash(floatBitsToUint(ubo.camera.view[0][2])) 
    */

    vec4 color = vec4(0.0);
    Ray ray;

    for(int i = 0; i < rays_per_pixel; i++){
        ray = get_ray();
        color += ray_color(ray);
    }

    // Calculate color of pixel
    color = color/rays_per_pixel;
    //color = normalize(vec4(random(),random(),random(),0.0)); // Visual rng test

    // Gamma correction
    color = vec4(pow(color.xyz, vec3(1.0/2.2)), 1.0);

    // Frame accumulation
    uint idx = pixelCoords.y * imageSize.x + pixelCoords.x;
    if (pc.reset_frame_accumulation) {
        accumulated_colors[idx] = vec4(0.0);
        sample_counts[idx] = 0;
    }
    accumulated_colors[idx] += color;
    sample_counts[idx] += 1;
    vec4 final_color = accumulated_colors[idx] / max(1, sample_counts[idx]);

    // Store color
    imageStore(outputImage, pixelCoords, final_color.zyxw);
}